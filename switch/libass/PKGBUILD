# Maintainer: Dave Murphy <davem@devkitpro.org>
# Contributor: carstene1ns <dev f4ke de>

pkgbasename=libass
pkgname=switch-$pkgbasename
pkgver=0.17.2
pkgrel=1
pkgdesc='A portable subtitle renderer (Nintendo Switch port)'
arch=('any')
url="https://github.com/libass/libass"
license=('custom: ISC')
options=(!strip libtool staticlibs)
depends=('switch-freetype' 'switch-libfribidi')
makedepends=('dkp-toolchain-vars' 'switch-pkg-config')
source=("$url/releases/download/$pkgver/$pkgbasename-$pkgver.tar.gz")
groups=('switch-portlibs')

build() {
  cd $pkgbasename-$pkgver

  source /opt/devkitpro/switchvars.sh

  ./configure --prefix="$PORTLIBS_PREFIX" --host=aarch64-none-elf \
    --disable-shared --enable-static \
    --enable-asm --enable-large-tiles \
    --disable-require-system-font-provider

  make
}

package() {
  cd $pkgbasename-$pkgver

  source /opt/devkitpro/switchvars.sh

  make DESTDIR="$pkgdir" install

  # license
  install -Dm644 "COPYING" "$pkgdir/$PORTLIBS_PREFIX/licenses/$pkgname/COPYING"
}

sha256sums=('a9afb52bf76a2569263fe2038896774c991b35c0968342a03be708e56ea60c3b')
